#!/usr/bin/env cdn-monitor-i
# -t 0:0.01:10 -m p1.y -m p2.y -m p3.y

## Example of point masses on virtual pogo sticks.
## The event system is used to switch between two
## dynamics, one for in the air (ballistic motion)
## and one for on the ground where the point mass
## is subject to a spring/damper acting on it.

integrator {
    method = "runge-kutta"
}

templates {
    node "pogopoint" {
        initial-state "air"

        ## The length of the virtual pogo stick
        pogolength = 0.1

        ## The number of times to pogo stick has bounced
        bounced = 0

        ## Initial state value
        y = "1"

        ## Mass of the point mass
        m = 1

        ## Spring constant
        K = 1000

        ## Damping constant
        D = 1

        ## Spring force of the ball when it's being compressed
        fspring = "K * (pogolength - y)"

        ## Acceleration of y due to gravity
        y'' = "-m * g"

        ## Acceleration of y due to the spring force. This term is only active when
        ## the pogo stick is in contact with the ground, which in turn is
        ## handled using the event system
        y'' = "fspring - D * y'" state "ground"

        ## Transfer from the air to the ground when y becomes smaller than the
        ## pogo stick length
        event "air" to "ground" when "y < pogolength" within 0.001 {
            # Keep track of the number of times we bounced
            set bounced = "bounced + 1"
        }

        ## Transfer from ground to air when y becomes larger than the pogo stick
        ## length
        event "ground" to "air" when "y > pogolength" within 0.001 {}
    }
}

## Gravity
g = 9.81

# Instantiation of pogo points with various parameters
node "p{1:3}" : pogopoint {
    y = "rand(1, 3)"
    m = [0.6, 0.3, 0.4]
    K = [1200, 500, 600]
    D = [1, 4, 5]
}

# vi:ts=4:et
