#!/usr/bin/env cdn-monitor-i
# -t 0:0.01:10 -m p1.y -m p2.y -m p3.y

integrator {
    method = "runge-kutta"
}

templates {
    node "pogopoint" {
        initial-state "air"

        pogolength = 0.1
        bounced = 0

        y = "1"
        m = 1

        # Spring damper
        K = 1000
        D = 1

        # Spring force of the ball when it's being compressed
        fspring = "K * (pogolength - y)"

        # Acceleration of y due to gravity
        y'' = "-m * g"

        # Acceleration of y due to the spring force. This term is only active when
        # the pogo stick is in contact with the ground, which in turn is
        # handled using the event system
        y'' = "fspring - D * y'" state "ground"

        # Transfer from the air to the ground when y becomes smaller than the
        # pogo stick length
        event "air" to "ground" when "y < pogolength" within 0.001 {
            # Keep track of the number of times we bounced
            set bounced = "bounced + 1"
        }

        # Transfer from ground to air when y becomes larger than the pogo stick
        # length
        event "ground" to "air" when "y > pogolength" within 0.001 {}
    }
}

g = 9.81

node "p{1:3}" : pogopoint {
    y = "rand(1, 3)"
    m = [0.6, 0.3, 0.4]
    K = [1200, 500, 600]
    D = [1, 4, 5]
}

# vi:ts=4:et
