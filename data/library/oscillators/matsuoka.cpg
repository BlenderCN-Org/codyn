templates
{
    ## Basic matsuoka oscillator
    group "oscillator"
    {
        [proxy]
        state "state"
        {
            ## State variable x1
            x1 = "x1_0" | integrated

            ## State variable v1
            v1 = "v1_0" | integrated

            ## State variable x2
            x2 = "x2_0" | integrated

            ## State variable v2
            v2 = "v2_0" | integrated

            x1_0 = "rand(-0.1, 0.1)" | once
            x2_0 = "rand(-0.1, 0.1)" | once
            v1_0 = "rand(-0.1, 0.1)" | once
            v2_0 = "rand(-0.1, 0.1)" | once

            y1 = "max(x1, 0)"
            y2 = "max(x2, 0)"

            c = "1"
            beta = "2"
            gamma = "2"

            tau1 = "0.1"
            tau2 = "0.2"

            ## The output of the oscillator
            output = "y1 - y2"
        }

        link "integrate" on "state"
        {
            x1 <= "1/tau1 * (c - x1 - beta * v1 - gamma * y2)"
            x2 <= "1/tau1 * (c - x2 - beta * v2 - gamma * y1)"
            v1 <= "1/tau2 * (y1 - v1)"
            v2 <= "1/tau2 * (y2 - v2)"
        }
    }

    link "coupling"
    {
        h = "1"
        g = "1"

        x1 <= "-(h * max(0, g))"
        x2 <= "-(h * -min(0, g))"
    }

    layout
    {
        "oscillator" at (@[++], 0)
        "coupling" at (@[++], 0)
    }
}

# vi:ex:ts=4:et
