templates
{
    include "hopf_functions.cdn"

    node "oscillator"
    {
        ## r node variable of Hopf oscillator
        ṙ = "γ ∙ (μ - r2) ∙ r"
        r = 0

        ## theta node variable of Hopf oscillator
        φ̇ = "ω"

        ## Desired amplitude
        ## Unused if you define mu directly.
        target_amplitude = 1

        ## Desired frequency
        target_frequency = 1

        ## Desired μ. Controls the Hopf bifurcation.
        ## With positive μ, the dynamical system will oscillate with amplitude
        ## √(μ). With negative μ, the system will have a stable fix point at
        ## r = 0.
        μ = "target_amplitude²"

        ## Gain on amplitude
        γ = 1

        ## Angular frequency
        ω = "2 ∙ π ∙ target_frequency"

        ## Amplitude squared
        r2 = "r²"

        ## Output variable
        χ = "r ∙ cos(φ)"
    }
}

# vi:ex:ts=4:et
