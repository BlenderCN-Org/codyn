## Reference: Kiyotoshi Matsuoka, Biol Cybern (2011) 104:297â€“304
## DOI 10.1007/s00422-011-0432-z

templates
{
    import "generic.cdn" as "generic"

    ## Parameters for the Matsuoka oscillator dynamics
    ## Split out from the 'oscillator' node to be appliable to another node
    node "parameters"
    {
        a = "2"
        b = "2"
        c = "1"

        tau = "0.1"
        Tau = "0.2"

        ## True if the condition for stable oscillations is satisfied.
        stable_oscillation = "a > 1 + tau / Tau && a < 1 + b"

        ## The ratio Ay / Ax of x and y amplitudes.
        ## It is also a measure of the error on frequency and amplitude estimates.
        ## Low values indicate bad estimates. A perfect estimate would yield a value of 1.
        kn = "(tau + Tau) / (Tau * a)"

        ## An estimate for the frequency of the isolated oscillator.
        frequency_estimate = "sqrt(((tau + Tau) * b - tau * a) / (tau * a)) / (Tau * 2 * pi)"

        ## An estimate for the amplitude of the isolated oscillator.
        amplitude_estimate = "c / (2 * (tau + Tau) / (Tau * a) - 1 + 2 / pi * (a + b) * asin(kn))"
    }

    ## Basic Matsuoka neuron
    node "neuron"
    {
        [proxy]
        node "node"
        {
            ## State variable x
            x = "rand(-0.1, 0.1)" | integrated

            ## State variable v
            v = "rand(-0.1, 0.1)" | integrated

            ## Neuron output
            y = "max(x, 0)"
        }

        ## Neuron dynamics
        edge "neuron_dynamics" on "node"
        {
            ## X dynamics targeting c, with adaptation through v
            x <= "1 / tau * (c - output.x - b * output.v)"
            
            ## Adaptation dynamics
            v <= "1 / Tau * (output.y - output.v)"
        }
    }

    ## Inhibition between neurons
    edge "inhibition"
    {
        x <= "-1 / tau * a * input.y"
    }

    ## Basic Matsuoka oscillator made of 2 neurons
    node "oscillator" : "parameters"
    {
        ## One of the half-center neurons
        node "neuron{1,2}" : "neuron" {}

        apply "generic"."interface-children-numeric"

        [bidirectional]
        edge from "neuron1" to "neuron2" : "inhibition" {}

        ## The output of the oscillator
        output = "y1 - y2"

        ## Estimated amplitude
        amplitude = "amplitude_estimate"

        ## Estimated frequency
        frequency = "frequency_estimate"

        layout
        {
            type at @[@0++], $(@0[?] * 5)
        }
    }

    ## Example coupling to a Matsuoka oscillator
    edge "coupling"
    {
        strength = "1"
        signal = "1"

        x1 <= "-(strength * max(0, signal))"
        x2 <= "-(strength * -min(0, signal))"
    }

    layout
    {
        type at @[@0++], $(@0[?] * 5)
    }
}

# vi:ex:ts=4:et
