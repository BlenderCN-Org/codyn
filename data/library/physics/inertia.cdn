node "Inertia"
{
    ## Inertia matrix of a box.
    ##   m: mass of the box.
    ##   w: width of the box (x).
    ##   h: height of the box (y).
    ##   d: depth of the box (z).
    Box(m, w, h, d) = "[
        1 / 12 ∙ m ∙ (h² + d²), 0,                      0;
        0,                      1 / 12 ∙ m ∙ (w² + d²), 0;
        0,                      0,                      1 / 12 ∙ m ∙ (w² + h²)
    ]"

    Sym(ixx, ixy, ixz, iyy, iyz, izz) = "[
        ixx, ixy, ixz;
        ixy, iyy, iyz;
        iyz, iyz, izz
    ]"

    ## Diagonal inertia matrix.
    ##   c: the diagonal inertia component.
    Diag(c) = "diag([c, c, c])"

    ## Inertia matrix of a sphere.
    ##   m: the mass of the sphere.
    ##   r: the radius of the sphere.
    Sphere(m, r) = "Diag(2 / 5 ∙ m ∙ r²)"

    ## Inertia matrix of a cylinder.
    ##   m: the mass of the cylinder.
    ##   r: the radius of the cylinder.
    ##   h: the height of the cylinder (z).
    Cylinder(m, r, h) = "[
        1 / 12 ∙ m ∙ (3 ∙ r² + h²), 0,                          0;
        0,                          1 / 12 ∙ m ∙ (3 ∙ r² + h²), 0;
        0,                          0,                          0.5 ∙ m ∙ r²
    ]"

    ## Inertia matrix of a rod.
    ##   m: the mass of the rod.
    ##   l: the length of the rod (y).
    Rod(m, l) = "[
        1 / 12 ∙ m ∙ l², 0, 0;
        0,               0, 0;
        0,               0, 1 / 12 ∙ m ∙ l²
    ]"

    Rotate(I, R) = "R * I * transpose(R)"

    RotateX_(I, cθ, sθ) = "Rotate(I, [
        1,  0,  0;
        0,  cθ, sθ;
        0, -sθ, cθ
    ])"
    RotateX(I, θ) = "RotateX_(cos(θ), sin(θ))"

    RotateZ_(I, cθ, sθ) = "Rotate(I, [
         cθ,  sθ, 0;
        -sθ,  cθ, 0;
         0,   0,  1
    ])"

    RotateZ(I, θ) = "RotateZ_(I, cos(θ), sin(θ))"

    RotateY_(I, cθ, sθ) = "Rotate(I, [
        cθ, 0, -sθ;
        0,  1,  0;
        sθ, 0,  cθ
    ])"

    RotateY(I, θ) = "RotateY_(I, cos(θ), sin(θ))"

    Translate_(m, I, C) = "I + m * (transpose(C) * C)"
    Translate(m, I, r) = "Translate_(m, I, [ 0,    -r[2],  r[1];
                                             r[2],  0,    -r[0];
                                            -r[1],  r[0],  0])"

    include "layout/line.cdn"
}

# vi:ex:ts=4:et
