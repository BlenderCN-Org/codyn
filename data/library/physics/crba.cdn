[once] include "findbodies.cdn"

# Define H for diagonal (i.e. bodies themselves)
node @bodies
{
    IC = "zeros(6, 6)"

    "fh_@0[!]" = "IC ∙ motionSubspace"
    "H_@0[!]" = "motionSubspaceT ∙ fh_@@0[!]"

    [self]
    edge
    {
        IC <= "spI"
    }
}

# Compute the mass matrix for each body, up to each of its
# ancestors
[each(@bodies)]
{
    # Select all the dynamics (up) edges in the chain from the current body
    # to the root
    [with("@0" | recurse(outputs | has-template(physics.dynamics) | output) | outputs | has-template(physics.dynamics))]
    {
        # Initialize variables in the output of the dynamics node for the
        # effects of the current node on each output node in the up chain
        node | output
        {
            "fh_@@0[!]" = "zeros(6, @bodydof[@@@0[!]])"
            "H_@@0[!]" = "motionSubspaceT ∙ fh_@@@0[!]"
        }

        # In all those dynamic edges, add a calculation for fh_@@0 (i.e. this)
        # is then recursively calculated from bottom to top
        edge | edges
        {
            "fh_@@0[!]" <= "transformT ∙ fh_@@@0[!]"
        }
    }
}

# Add computation of IC in the dynamics edge
edge has-template("physics" . "dynamics")
{
     IC <= "transformT ∙ IC ∙ transform"
}

# vi:ex:ts=4:et
