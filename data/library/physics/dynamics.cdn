include "rnea.cdn"
include "crba.cdn"

[once] include "findbodies.cdn"

node "dynamics"
{
    ## Generalized forces
    τ = "zeros(@numdof, 1)"

    ## C matrix
    C = "zeros(@numdof, 1)"

    ## Mass matrix
    H = "zeros(@numdof, @numdof)"

    ## Generalized accelerations
    ddq = "slinsolve(H, τ - C, parents)"

    ## Jacobian
    #J = "zeros(6, @numdof)"

    ## Parent indices
    parents = "[{@parentsdof||@0;@1}]"
}

node "@bodies"
{
    # Jacobian selection matrix
    #JS = "[{@bodies|@bodychain[@0[!]]||@0;@1}]"
}

# Fill in H for all relevant body relationships
edge "HC_@bodies" from @1 . variables | /fh_([0-9]+)/ | parent | unique to "dynamics"
{
    defines
    {
        jc = "@{@bodydofidx[@@@@@0[!]]:+$(@bodydof[@@@@@0[!]] - 1)||@0\,@1}"
        jr = "@{@jc||@0;@1}"
    }

    [each(self | input . variables | /fh_([0-9]+)/)]
    {
        defines
        {
            ic = "@{@bodydofidx[@@1]:+$(@bodydof[@@1] - 1)||@0\,@1}"
            ir = "@{@ic||@0;@1}"
        }

        # Select block in H to write H_@i into
        "H[[@ir], [@jc]]" <= "H_@@1"

        [ifstr("$$(@1 != @@@0[!],1,)")]
        {
            [ifstr("$$(!@only_lower_H[?],1,)")]
            {
                "H[[@jr], [@ic]]" <= "H_@@@@1"
            }
        }
    }

    # Fill column of the jacobian
    #"J[[{0:5||@0;@1}], @jc]" <= "J"

    # Fill C
    "C[@jr]" <= "C"
}

# Integrate accelerations on bodies
[each(@bodies)]
{
    defines
    {
        j = "@{@bodydofidx[@@0[!]]:+$(@bodydof[@@0[!]] - 1)||@0;@1}"
    }

    edge "acceleration_@0" from "dynamics" to "@@@0"
    {
        ddq <= "ddq[[@j]]"
    }

    edge "force_@0" from "@@0" to "dynamics"
    {
        "τ[[@j], 0]" <= "τ"
    }
}

# vi:ex:ts=4:et
