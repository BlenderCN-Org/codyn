network
{
	g = "9.81"
}

group "pendulum"
{
	defines
	{
		# Define set of general coordinates q
		qn = "x,phi"

		q, q_count, qe = "q_{@qn}"

		q = "{@qe||@0\,@1}"

		# Define utilities for expansion
		dq  = "{@q|@0'||@0\,@1}"
		ddq = "{@dq|@0'||@0\,@1}"
		qdq = "{@q,@dq||@0\,@1}"
		qdqddq = "{@q,@dq,@ddq||@0\,@1}"
		vq = "m,l"

		# Escaped versions
		_, _, "{q,dq,ddq,qdq,qdqddq,vq}e" = "{@[@1]}"
	}

	functions
	{
		# Mass locations
		Mx(@qe)(@vqe) = "q_x + l * sin(q_phi)"
		My(@qe)(@vqe) = "-l * cos(q_phi)"

		# Mass velocities
		DMx(@qdqe)(@vqe) = "diff[Mx](@q, @dq)"
		DMy(@qdqe)(@vqe) = "diff[My](@q, @dq)"

		# Kinetic energy
		T(@qdqe)(@vqe) = "0.5 * m * sqsum(DMx(@qdq), DMy(@qdq))"

		# Potential energy
		V(@qe)(@vqe) = "-m * l * g * cos(q_phi)"

		# Partial of kinetic energy towards generalized coordinate velocities
		"DTdd@qe"(@qdqe)(@vqe) = "simplify[pdiff[T, @1']](@qdq)"

		# Partial of kinetic and potential energy towards generalized coordinates
		"D{T,V}d@qe"(@qdqe)(@vqe) = "simplify[pdiff[@1, @2]](@qdq)"

		# Total differential of kinetic energy towards time
		"DdTdd@[qe]dt"(@qdqddqe)(@vqe) = "simplify[diff[DTdd@1]](@qdqddq)"

		# Solve for accelerations
		Acceleration(@qdqe, idx)(@vqe) = "linsolve[{@q|DdTdd@[0]dt||@0\,@1};@ddq][idx](@qdq)"
	}

	state "s"
	{
		@qe' = "dq_@1"
		@qe'' = "Acceleration(@qdq, @1[!])"

		l = 1
		m = 1
	}
}

